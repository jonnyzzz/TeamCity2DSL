buildscript {
  ext.kotlin_version = '1.0.0'
  ext.kotlin_xml_dsl_version = '0.1.5'
  ext.kotlin_xml_bind_version = '0.1.6'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.5"
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  group = 'org.jonnyzzz.teamcity.dsl'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jonnyzzz/maven" }
  }

  version = rootProject.hasProperty('teamcity') ? rootProject['teamcity']['build.number'] : 'SNAPSHOT'

  afterEvaluate {
    configurations.all.each {
      it.allDependencies.withType(ProjectDependency).each {
        publishToMavenLocal.dependsOn it.dependencyProject.tasks.jar
        publishToMavenLocal.dependsOn it.dependencyProject.tasks.publishToMavenLocal
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}

task bintrayUploadEx {
  dependsOn ':DSL:bintrayUpload'
}

task localUploadEx {
  dependsOn ':DSL:publishToLocalMaven'
}

buildscript {
  ext.kotlin_version = '1.0.0'
  ext.kotlin_xml_dsl_version = '0.1.5'
  ext.kotlin_xml_bind_version = '0.1.6'

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.5"
}

version = rootProject.hasProperty('teamcity') ? rootProject['teamcity']['build.number'] : 'SNAPSHOT'

subprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'maven'
  apply plugin: 'maven-publish'

  group = 'org.jonnyzzz.teamcity.dsl'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenCentral()
    maven { url "http://dl.bintray.com/jonnyzzz/maven" }
  }

  afterEvaluate {
    configurations.all.each {
      publishToMavenLocal.dependsOn it

      it.allDependencies.withType(ProjectDependency).each {
        publishToMavenLocal.dependsOn it.dependencyProject.tasks.classes
        publishToMavenLocal.dependsOn it.dependencyProject.tasks.jar
        publishToMavenLocal.dependsOn it.dependencyProject.tasks.publishToMavenLocal
      }
    }

    configurations.all.each {
      bintrayUpload.dependsOn it

      it.allDependencies.withType(ProjectDependency).each {
        bintrayUpload.dependsOn it.dependencyProject.tasks.bintrayUpload
      }
    }
  }
  publishToMavenLocal.inputs.file project.buildFile

  tasks.withType(Test).each { test ->
    test.outputs.upToDateWhen { false }

    if (System.getenv("TRAVIS_CI") != null) {
      test.testLogging.showStandardStreams = true
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}

task bintrayUploadEx {
  dependsOn ':src:gradle-plugin:bintrayUpload'
}

task localUploadEx {
  dependsOn ':src:gradle-plugin:publishToMavenLocal'
}

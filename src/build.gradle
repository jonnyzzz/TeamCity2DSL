subprojects {

  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile "junit:junit:4.12"
  }

  test {
    useJUnit()
  }
}

//publishing
subprojects {
  apply plugin: 'maven-publish'
  apply plugin: "com.jfrog.bintray"

  task sourceJar(type: Jar) {
    from sourceSets.main.allSource
  }

  println("Updating project maven: $project.path. ${project.hasProperty('teamcity2dsl_notForMaven')}")

  publishing {
    publications {
      mavenJava(MavenPublication) {
        from components.java
        artifact sourceJar {
          classifier "sources"
        }
      }
    }
  }

  //register dependency
  rootProject.localUploadEx.dependsOn publishToMavenLocal

  if (rootProject.hasProperty("bintrayUser")) {
    bintray {
      user = getProperty('bintrayUser')
      key = getProperty('bintrayApiKey')
      publications = ['mavenJava']
      pkg {
        repo = 'maven'
        name = 'teamcity2dsl'
        userOrg = getProperty('bintrayUser')
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jonnyzzz/TeamCity2DSL'
      }
    }

    rootProject.bintrayUploadEx.dependsOn bintrayUpload
  }
}

configure(project(':src:gradle-plugin')) {
  apply plugin: "com.gradle.plugin-publish"
  pluginBundle {
    website = 'https://github.com/jonnyzzz/TeamCity2DSL'
    vcsUrl = 'https://github.com/jonnyzzz/TeamCity2DSL'
    description = 'teamcity2dsl'
    tags = ['TeamCity', 'DSL', 'Kotlin']

    plugins {
      teamcity2dsl {
        id = 'org.jonnyzzz.teamcity.dsl'
        displayName = 'TeamCity2DSL'
      }
    }
  }

  rootProject.bintrayUploadEx.dependsOn tasks.publishPlugins
}
